buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'java'
apply plugin: 'jacoco'


group 'com.github.priytam'
version "${AUTO_VESRION}"
description = """\
Automation tool to write tests for Http Api, Web Page Action, and Command
Line based application with embedded mock server and highly configurable reports
------------------------------------------
Project version: ${version}
Gradle version: ${gradle.gradleVersion}
Documentation: https://priytam.github.io/auto/
------------------------------------------
"""

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        repositories {
            mavenCentral()
            jcenter()
        }
    }
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'
        apply plugin: 'jacoco'
        apply plugin: 'jacoco'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    /*repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }*/
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/priytam/auto'

                        scm {
                            connection 'scm:git:https://github.com/priytam/auto.git'
                            developerConnection 'scm:git:git@github.com:/priytam/auto.git'
                            url 'https://github.com/priytam/auto.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'pjpandey'
                                name = 'Priytam Jee Pandey'
                                email = 'mrpjpandey@gmail.com'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination(file("${buildDir}/reports/jacoco/report.xml"))
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

/* use this command to publish library ./gradlew build -Prelease uploadArchives */